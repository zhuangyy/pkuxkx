#class caiqi open;

#alias {on_caiqi_finish} {
    #alias tmp_caiqi_finish #cr;
    #alias tmp_caiqi_finish %0;
    #class on_caiqi_finish.inner open;

    #action {{大脑一片空明，感觉一股热气在任督二脉盘旋|仅子午两个时辰能够进行采气|每天子午各采气一次就够了}} {
        #class on_caiqi_finish.inner kill;
        #delay {caiqi_finish} {
          tmp_caiqi_finish;
        } {3};
    };

    #class on_caiqi_finish.inner close;
};

#alias {caiqi.action} {
    gt 三清殿;
    on_there {
        ask song about 下山;
        gt 天柱峰下;
        on_there {
          caiqi;
        };
    };
};

#var caiqi_done 0;
#alias {caiqi.auto} {
    #alias tmp_caiqi_auto #cr;
    #alias tmp_caiqi_auto %0;
    #class caiqi.auto.inner open;

    #if {"${curr_daytime}" == "正午" || "${curr_daytime}" == "午夜"} {
        #if {"${caiqi_done}" == "0" && "${char_menpai}" == "wudang"} {
            show_to_info 采气(${curr_daytime});
            caiqi.action;
            on_caiqi_finish {
                show_to_info 采气结束(${curr_daytime});
                #var caiqi_done 1;
                tmp_caiqi_auto;
            };
        };
        #else {
            tmp_caiqi_auto;
        };
    };
    #else {
        #var caiqi_done 0;
        tmp_caiqi_auto;
    };
    #class caiqi.auto.inner close;
};

#alias {cq} {
    show_to_info 采气;
    caiqi.action;
    on_caiqi_finish {
        show_to_info 采气结束; 
    };
};

#class caiqi close;
