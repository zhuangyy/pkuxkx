#class xue open;
#tab xue.start;
#tab xue.stop;

#var xue_count 1;
#var xue_max   52;

#alias {xue.start} {
    #class xue.inner open;
    #var xue_current_level 0;

    #alias {xue.restart} {
        chifan;
        goto_shifu;
        on_there {
            start_ticker;
        };
    };

    #alias {xue.checklevel} {
        #class xue.checklevel.inner open;
        #action {│%s%S%s│${xue_gongfu}%s│%S│%s%d│%*} {
            #class xue.checklevel.inner kill;
            #var xue_current_level %%%7;
            show_to_info ${xue_gongfu}/${xue_current_level}/${xue_max};
            #if {${xue_max} <= ${xue_current_level}} {
                xue.stop;
                show_to_info 你的${xue_gongfu}已经满级了;
								#showme 你的${xue_gongfu}已经满级了;
            };
            #else {
                xue.restart;
            };
        };

        stop_ticker;
        #if {${xue_max} > 0} {
            #send {sk};
            #delay {3} {
            };
        };
        #else {
            #class xue.checklevel.inner kill;
            xue.restart;
        };
        #class xue.checklevel.inner close;
    };

    #alias {start_ticker} {
        #ticker {xue} {
				    #nop echo {xue ${shifu} for ${xue_gongfu} ${xue_count}};
            xue ${shifu} for ${xue_gongfu} ${xue_count};
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {xue};
    };

    #action {你今天太累了} {
        yun regenerate;
    };

    #action {你的%*进步了} {
        show_to_info %%1 进步了;
        xue.checklevel;
    };

    #action {你的内力不够} {
        stop_ticker;
        #delay {xue_delay} {
            on_there {
                sleep;
            };
            on_wakeup {
                xue.restart;
            };
            goto_rest;
        } {3};
    };

    #action {{你的潜能已经发挥到极限了|没有办法学习这种技能|也许是缺乏实战经验|切磋}} {
        xue.stop;
    };
    #class xue.inner close;

    autoheal.off;
    #var xue_gongfu %1;
    #var xue_count %2;
    show_to_quest 学习: ${shifu}/${xue_gongfu}/${xue_count};
    xue.checklevel;
    #nop xue.restart;
};

#alias {xue.stop} {
    #class xue.inner kill;
    #unticker {xue};
    show_to_quest 学习结束;
    autoheal.on;
};

#alias {xue.full} {
  #class xue.full.inner open;
  #var xue.full.idx 1;
	
	#alias {xue.full.start} {
  	#list gongfus get ${xue.full.idx} {gongfu};
		xue.start ${gongfu} ${xue_count};
	};

	#action {满级了} {
		#math xue.full.idx ${xue.full.idx} + 1;
		#list gongfus size xue.full.maxsize;
		#if {${xue.full.idx} > ${xue.full.maxsize}} {
			xue.full.stop;
		};
    #else {
			#delay {5} {
						xue.full.start;
			};
    };
  };
	#list gongfus create {dodge;force;cuff;parry};

  #var xue_count %1;
  show_to_quest 全面学习开始;
	xue.full.start;
  #class xue.full.inner close;
};

#alias {xue.full.stop} {
  xue.stop;
	#class xue.full.inner kill;
  show_to_quest 全面学习结束;
};

#class xue close;
