#nop 用法:
#nop 学单个武功: xue.start <武功名称> <使用点数>
#nop 停止学习: xue.stop
#nop 例如: xue.start force 10
#nop 设置学习武功的级别上限(到达指定上限后, xue会停止): xue.max <武功级别>
#nop 例如: xue.max 55
#nop 只输入 xue.max 会echo当前设置的上限. xue.max 0 取消上限.
#nop 批量学习: xue.full <使用点数>
#nop 例如: xue.full 10
#nop 停止批量学习: xue.full.stop


#class xue open;
#tab xue.start;
#tab xue.stop;

#var xue_count 10;
#var xue_max   0;

#alias {xue.max} {
    #if {"%1" != "" && %1 >= 0} {
        #var xue_max %1;
    };
    #echo {最高等级 -> ${xue_max}};
};

#alias {xue.start} {
    #class xue.inner open;
    #var xue_current_level 0;

    #alias {xue.restart} {
        chifan;
        goto_shifu;
        on_there {
            start_ticker;
        };
    };

    #alias {xue.checklevel} {
        #class xue.checklevel.inner open;
        #action {│%s%S%s│${xue_gongfu}%s│%S│%s%d│%*} {
            #class xue.checklevel.inner kill;
            #var xue_current_level %%%7;
            show_to_info ${xue_gongfu}/${xue_current_level}/${xue_max};
            #if {${xue_max} > 0 && ${xue_max} <= ${xue_current_level}} {
                show_to_info 学习:你的${xue_gongfu}满级了;
                xue.stop;
                #showme 学习:你的${xue_gongfu}满级了;
            };
            #else {
                xue.restart;
            };
        };

        stop_ticker;
        #if {${xue_max} > 0} {
            #send {sk};
            #delay {3} {
            };
        };
        #else {
            #class xue.checklevel.inner kill;
            xue.restart;
        };
        #class xue.checklevel.inner close;
    };

    #alias {start_ticker} {
        #ticker {xue} {
            #nop echo {xue ${shifu} for ${xue_gongfu} ${xue_count}};
            xue ${shifu} for ${xue_gongfu} ${xue_count};
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {xue};
    };

    #action {你今天太累了} {
        yun regenerate;
    };

    #action {你的%*进步了} {
        show_to_info %%1 进步了;
        xue.checklevel;
    };

    #action {你的内力不够} {
        stop_ticker;
        #delay {xue_delay} {
            on_there {
                sleep;
            };
            on_wakeup {
                xue.restart;
            };
            goto_rest;
        } {3};
    };

    #action {{你的潜能已经发挥到极限了|没有办法学习这种技能|也许是缺乏实战经验}} {
        xue.stop;
        #showme 学习:你的${xue_gongfu}满级了;
    };

    #class xue.inner close;

    autoheal.off;
    #var xue_gongfu %1;
    #var xue_count %2;
    show_to_quest 学习: ${shifu}/${xue_gongfu}/${xue_count};
    xue.checklevel;
    #nop xue.restart;
};

#alias {xue.stop} {
    #class xue.inner kill;
    #unticker {xue};
    show_to_quest 学习结束;
    autoheal.on;
};

#alias {xue.full} {
    #class xue.full.inner open;
    #var xue.full.idx 1;
    
    #alias {xue.full.start} {
        #list gongfus get ${xue.full.idx} {gongfu};
        xue.start ${gongfu} ${xue_count};
    };

    #action {学习:你的%*满级了} {
        #math xue.full.idx ${xue.full.idx} + 1;
        #list gongfus size xue.full.maxsize;
        #if {${xue.full.idx} > ${xue.full.maxsize}} {
            xue.full.stop;
        };
        #else {
            #delay {5} {
                xue.full.start;
            };
        };
    };
    #list gongfus create {taiji-shengong};

    #var xue_count %1;
    show_to_quest 全面学习开始;
    xue.full.start;
    #showme 全面学习开始;

    #class xue.full.inner close;
};

#alias {xue.full.stop} {
  xue.stop;
  #class xue.full.inner kill;
  show_to_quest 全面学习结束;
  #showme 全面学习结束;
};

#class xue close;
