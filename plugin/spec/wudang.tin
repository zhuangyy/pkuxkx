#class wudang open
#var jiali_count 50;

#alias {attack.prepare} {
    yun powerup;
};

#alias {attack.xf.combined} {
	#var jiali_count max;
	pfm wuying;
	pfm gangrou;
	#if {${mj_started} != 1} {
	    pfm lzj;
	};
	#class attack.inner open;
	#if {${mj_started} == 1} {
		#ticker {wuying} {
			pfm wuying;
		} {5.8};
	};
	#else {
		#ticker {wuying} {
			pfm wuying;
		} {2.8};
		#ticker {lzj} {
			pfm lzj;
		} {3.8};
	};
	#ticker {gr} {
		pfm gangrou;
	} {4.8};
	#class attack.inner close;
};

#alias {attack.xf} {
	pfm wuying;
	#class attack.inner open;
	#ticker {wuying} {
			pfm wuying;
	} {2.8};
	#class attack.inner close;
};

#alias {attack.auto} {
	#ticker {lzj} {
		pfm lzj;
	} {2.8};
};

#alias {attack} {
    pfm chan;
    pfm zong;

    #class attack.inner open;
    #ticker {perf_chan} {
        pfm chan;
    } {1};
    #class attack.inner close;
		
    #if {${xf.type} == 2 || ${xf.type == 4}} {
        #var jiali_count max;
        attack.xf;
    };
    #elseif {${xf.type} == 3} {
        attack.xf;
    };
    #elseif {${xf.type} == 1} {
        attack.xf.combined;
    };
    #else {
        attack.auto;
    };
};

#var shortcut F1:缠 F2:无影 F3:连字诀 F4:刚柔;
#macro {\eOQ} {
    wuying;
};

#macro {\eOR} {
    lzj;
};

#macro {\eOP} {
    chan;
};

#macro {\eOS} {
    gangrou;
};

#alias {chan} {
    unwi;
    do_wield sword;
    perform taiji-jian.chan;
    #send {perform taiji-jian.chan};
    #send {perform taiji-jian.chan};
    wi;
};

#alias {sanhuan} {
    unwi;
    do_wield sword;
    jiali ${jiali_count};
    perform taiji-jian.sanhuan;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#alias {zhen} {
    unwi;
    perform taiji-quan.zhen;
    wi;
};

#alias {zong} {
    #nop send {perform tiyunzong.zong};
};

#alias {lzj} {
    unwi;
    do_wield sword;
    jiali 10;
    perform taiji-jian.lian;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#alias {szj} {
    unwi;
    do_wield sword;
    perform taiji-jian.sui;
    wi;
};

#alias {wuying} {
    unwi;
    jiali ${jiali_count};
    perform claw.wuying;
    #if {${gangrou_ing} == 0} {
        jiali none;
    };
    wi;
};

#var gangrou_ing 0;
#alias {gangrou} {
    #var gangrou_ing 1;
    unwi;
    jiali ${jiali_count};
    perform taiji-quan.gangrou;
    #action {抬手就封住了你这一掌！} {
        #unaction {抬手就封住了你这一掌！};
        #delay {delay_gangrou} {
            #var gangrou_ing 0;
            jiali none;
        } {5};
    };
    #delay {delay_gangrou} {
        #unaction {抬手就封住了你这一掌！};
    };
    wi;
};

#alias {lianhuan} {
    perform spear.lianhuan;
};

#alias {goto_lingwu} {
    gt {静修阁};
};

#alias {goto_rest} {
    gt {武当山客房};
};

#alias {goto_shifu} {
    #nop gt {武当山小屋};
	gt {前院};
};

#alias {goto_mz} {
    gt {武当后堂};
};

#list my_weapons_to_wield create {sword;blade;};

#var shifu yu;

#alias {reborn} {
    #send {jifa cuff taiji-quan};
    #send {jifa dodge tiyunzong};
    #send {jifa parry taiji-quan};
    #send {jifa sword taiji-jian};
    #send {jifa blade xuanxu-dao};
    #send {jifa claw taiji-zhao};
    #nop #send {jifa force taiji-shengong};
    #nop send {jifa spear yuejia-qiangfa};
    bei cuff claw;
};

stat.register 缠 miss 只听得「铮」一声清响，剧烈碰撞之下你剑诀为敌所破，无以为继，被迫撤剑收招。;
stat.register 缠 hit 只能见招拆招，疲于应对，不觉已深陷太极剑意之中，无法出招。;

#var menzhong zhang;
#var menzhong_didian 武当后堂;

#alias {attack.end} {
    #var jiali_count 50;
};

#list fsk create {wd};
#class wudang close
