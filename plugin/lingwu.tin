#nop 用法:
#nop 领悟单个武功: lingwu.start <武功名称> <使用点数>
#nop   例如: lingwu.start force 10
#nop 停止领悟: lingwu.stop
#nop 设置领悟武功的级别上限(到达指定上限后, lingwu会停止): lingwu.max <武功级别>
#nop   例如: lingwu.max 55
#nop 只输入 lingwu.max 会echo当前设置的上限. lingwu.max 0 取消上限.
#nop 批量领悟: lingwu.full <使用点数>
#nop   例如: lingwu.full 10
#nop 停止批量领悟: lingwu.full.stop
#nop ++注意++ 
#nop lingwu.tin仅负责将基本功夫升级到指定级别, 不保证同时特殊功夫也到达同样级别, 那是lian.tin的任务

#class lingwu open;
#tab lingwu.start;
#tab lingwu.stop;

#var lingwu_count 10;
#var lingwu_max   0;

#alias {lingwu.max} {
    #if {"%1" != "" && %1 >= 0} {
        #var lingwu_max %1;
    };
    #echo {最高等级(v3) -> ${lingwu_max}};
};

#alias {lingwu.start} {
    #class lingwu.inner open;
    #var lingwu_current_level 0;
    #var lingwu_dolian  0;

    #alias {lingwu.restart} {
        chifan;
        goto_lingwu;
        on_there {
            unwield all;
            #if {"${lingwu_gongfu}" == "sword"} {
                wield sword;
            };
            #elseif {"${lingwu_gongfu}" == "blade"} {
                wield blade;
                wield dao;
            };
            start_ticker;
        };
    };

    #alias {lingwu.checklevel} {
        #class lingwu.checklevel.inner open;

        #action {│%s%S%s│${lingwu_gongfu}%s│%S│%s%d│%*} {
            #class lingwu.checklevel.inner kill;
            #var lingwu_current_level %%%7;
            
            show_to_info 领悟/练习: ${lingwu_gongfu}/${lingwu_current_level}/${lingwu_max};
            #if {${lingwu_max} > 0 && ${lingwu_max} <= ${lingwu_current_level}} {
                show_to_info 领悟:你的${lingwu_gongfu}满级了;
                lingwu.stop;
                #showme 领悟:你的${lingwu_gongfu}满级了;
            };
            #else {
                lingwu.restart;
            };
        };

        stop_ticker;
        #if {${lingwu_max} > 0} {
            #send {sk};
            #delay {3} {
            };
        };
        #else {
            #class lingwu.checklevel.inner kill;
            lingwu.restart;
        };
        #class lingwu.checklevel.inner close;
    };

    #alias {start_ticker} {
        #ticker {lingwu} {
            #nop echo {lingwu ${shifu} for ${lingwu_gongfu} ${lingwu_count}};
            #if {"${lingwu_dolian}" == "0"} {
              lingwu ${lingwu_gongfu} ${lingwu_count};
            };
            #else {
              lian ${lingwu_gongfu} ${lingwu_count};
            };
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {lingwu};
    };

    #action {你的%*进步了} {
        show_to_info %%1 进步了;
        lingwu.checklevel;
    };

    #action {无法静下心来领悟功夫} {
        yun regenerate;
    };

   #action {{你今天太累了|你的体力不够练|你现在气血虚浮|你的体力太低了}} {
        yun recover;
    };

    #action {{你的内力不够|你的真气不够|你现在内力不足}} {
        stop_ticker;
        #delay {lingwu_delay} {
            on_there {
                sleep;
            };
            on_wakeup {
                lingwu.restart;
            };
            goto_rest;
        } {3};
    };

    #action {{你的潜能已经发挥到极限了|没有办法学习这种技能|也许是缺乏实战经验}} {
        lingwu.stop;
        #showme 领悟:你的${lingwu_gongfu}满级了;
    };

    #action {你的基本功夫比你的高级功夫还高} {
        #showme 你该练习特殊技能了;
        #var lingwu_dolian 1;
        show_to_info 练习: ${lingwu_gongfu}/${lingwu_current_level}/${lingwu_max};
    };

    #action {你需要提高基本功} {
        #showme 你该领悟基本功了;
        #var lingwu_dolian 0;
        show_to_info 领悟: ${lingwu_gongfu}/${lingwu_current_level}/${lingwu_max};
    };

    autoheal.off;
    #var lingwu_gongfu %1;
    #var lingwu_count %2;
    show_to_quest 领悟: ${lingwu_gongfu}/${lingwu_count}/${lingwu_max};
    lingwu.checklevel;

    #class lingwu.inner close;
};

#alias {lingwu.stop} {
    #class lingwu.inner kill;
    #unticker {lingwu};
    show_to_quest 领悟结束;
    autoheal.on;
};

#alias {lingwu.full} {
  #class lingwu.full.inner open;
  #var lingwu.full.idx 1;
    
  #alias {lingwu.full.start} {
    #list gongfus get ${lingwu.full.idx} {gongfu};
    lingwu.start ${gongfu} ${lingwu_count};
  };

  #action {领悟:你的%*满级了} {
    #math lingwu.full.idx ${lingwu.full.idx} + 1;
    #list gongfus size lingwu.full.maxsize;
    #if {${lingwu.full.idx} > ${lingwu.full.maxsize}} {
        lingwu.full.stop;
    };
    #else {
        #delay {5} {
            lingwu.full.start;
        };
    };
  };
  #list gongfus create {cuff;claw;dodge;force;parry;sword;blade};

  #var lingwu_count %1;
  show_to_quest 全面领悟开始;
  #showme 全面领悟开始;
  lingwu.full.start;
  #class lingwu.full.inner close;
};

#alias {lingwu.full.stop} {
  lingwu.stop;
  #class lingwu.full.inner kill;
  show_to_quest 全面领悟结束;
  #showme 全面领悟结束;
};

#class lingwu close;
