#class myfsk open;

#alias {tune.off} {
    #class tune.off.inner open;
    
    #action {0(nation)} {
        #class tune.off.inner kill;
    };

    #action {(%S)%s〖开〗} {
        #echo {turn off %%1};
        tune %%1;
    };

    #send tune;
    #class tune.off.inner close;
};

#alias {tune.on} {
    tune.off;
    tune rw;
    tune jh;
    tune bd;
    tune qy;
    tune helpme;
    tune chat;
};

#alias {myfsk.start} {
    #class myfsk.inner open;

    #var myfsk.gongfumax 135;
    #var myfsk.gongfumax %1;
    #math myfsk.dzmax ${myfsk.gongfumax} * 15 - 10;
    #math myfsk.dzpot (${myfsk.gongfumax} * 15 * 4 / 100);

    #action {全面学习结束} {
        ds.gongfumax none ${myfsk.gongfumax};
        gt bs;
        on_there {
            getout 2;
            getout 2;
            ds.full 5;
        };
    };

    #action {全面读书结束} {
        store tianlong;
        store shediao;
        ds.gongfumax dazuo ${myfsk.dzmax};
        ds.start dazuo for ${myfsk.dzpot};
    };

    #action {读书:你的dazuo满级了} {
        ds.gongfumax tuna ${myfsk.dzmax};
        ds.start tuna for ${myfsk.dzpot};
    };

    #action {读书:你的tuna满级了} {
        lingwu.max ${myfsk.gongfumax};
        lingwu.full 5;
    };

    #action {全面领悟结束} {
        lian.full 5;
    };

    #action {全面练习结束} {
        #class myfsk.inner kill;
    };

    gt bs;
    on_there {
        get all;
        #delay {3} {
            xue.max ${myfsk.gongfumax};
            xue.full 20;
        };
    };

    #class myfsk.inner close;
};

#alias {myfsk.stop} {
    #class myfsk.inner kill;
    gt.stop;
    xue.full.stop;
    ds.full.stop;
    ds.stop;
    lingwu.full.stop;
    lian.full.stop;
};

#class myfsk close;
